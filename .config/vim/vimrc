if !has('nvim')			" Settings that only need to apply to original vim
	set nocompatible	" Vi incompatible settings (must be first as it affects other options)

	set runtimepath=$XDG_CONFIG_HOME/vim,$VIM,$VIMRUNTIME,$XDG_CONFIG_HOME/vim/after	" .vim config directory
	set directory=~/.cache/vim/swap//,/tmp		" swap file directory
	set history=1000				" lines of command and search history kept
	set viminfo+=<1000				" possible to copy up to 1000 lines (default 50)
	set viminfo+=n~/.local/share/vim/viminfo	" viminfo directory, old sessions will use and recreate old location, this MUST BE THE LAST SET VIMINFO
	set packpath+=$XDG_CONFIG_HOME/vim		" location to find the pack directory where plugins are installed

	set backspace=indent,eol,start			" allow backspacing over everything in insert mode
	set autoindent					" auto indent on newline if starting from indent
	set showcmd					" display incomplete commands in bottom right
	set laststatus=2				" always show statusline
	set hlsearch					" search result highlighting
	set incsearch					" show matches while typing search pattern
endif



set hidden		" allow multiple files by hiding instead of closing
set linebreak		" wrap lines at end of word rather than mid
set ttimeoutlen=0	" allows exiting insert mode instantly and removes lag on O, may cause issues with key combinations
set spelllang=en_ca,cjk " Canadian spellcheck, ignore east asian
set ignorecase		" case insensitive searching
set smartcase		" disable ignorecase when inputting uppercase
set mouse=a		" enable mouse in all modes

set scrolloff=5		" minimum visible lines above and below cursor
set number		" number lines
set relativenumber	" lines are numbered relative to current line, useful for calculating motions
set cursorline		" highlight current line
set shortmess+=I	" disable intro message when opening vim

set statusline=%<%F\ (%LL)\ %#WarningMsg#%M%#ErrorMsg#%R%##%=%y[%{&ff}][%{&fenc}]\ %4P

set backup		" keep a backup file
set backupdir=$XDG_CACHE_HOME/vim/backup
" set backup files to contain timestamp in format _YYYYMMDD-HHMM
au BufWritePre * let &backupext = '_' . strftime("%Y%m%d-%H%M")


syntax on		" syntax highlighting
colorscheme custom_dark

" automatically enable the Colorizer plugin for certain filetypes, note it's laggy for huge files
" let g:colorizer_auto_filetype='css,html'


" set leader key to something else so we can use backslash without conflict
let mapleader="Z"

" toggle spellcheck
map s :set spell! spell?<CR>

" turn off current search highlighting
map <silent> ` :nohlsearch<CR>

" browse current working directory
map <silent> <F1> :e .<CR>

" browse directory of current file
map <silent> <F2> :e %:p:h<CR>

" show buffer list using BufExplorer
map <silent> <F3> :BufExplorer<CR>

" toggle colorcolumn
map <F4> :call ColorColumnToggle()<CR>
function! ColorColumnToggle()
	if &colorcolumn
		setlocal cc=0
	else
		setlocal cc=81
	endif
endfunction

" show marks, show registers
map <F5> :marks<CR>
map <F6> :registers<CR>

" toggle relative line numbering
map <F7> :set relativenumber! relativenumber?<CR>

" preview colour scheme
map <F8> :so $VIMRUNTIME/syntax/hitest.vim<CR>

" open CtrlP buffer list
map <Space> :CtrlPBuffer<CR>
" open CtrlP MRU list.
map <F9> :CtrlPMRU<CR>
" open CtrlP MRU list. for some reason NUL is ctrl-space
"map <NUL> :CtrlPMRU<CR>
" unmap ctrl-space from insert mode as it does weird stuff
"imap <NUL> <Nop>

" toggle Colorizer plugin
map <F10> :ColorToggle<CR>

" backspace and backslash go backwards and forwards in cursor location
noremap <BS> <C-o>
noremap <Bslash> <C-i>

" tab switches between last used file (same as ctrl-6)
noremap <Tab> <C-^>

" exit on q
map q :q<CR>

" replace Q EX mode with record macro
noremap Q q

" change buffers
map <silent> - :bprev<CR>
map <silent> = :bnext<CR>

" faster substitute command
map K :%s/

" ctrl-U in insert mode cannot be undone unless we break the undo with ctrl-g u
inoremap <C-U> <C-G>u<C-U>


" auto fix following words in insert mode (ctrl-v at the end of word to avoid auto fix)
iabbrev teh the


" always jump to the last known cursor position unless invalid or first line
autocmd BufReadPost *
    \ if line("'\"") > 1 && line("'\"") <= line("$") |
    \   exe "normal! g`\"" |
    \ endif


" command to see unsaved changes to current file
if !exists(":DiffOrig")
  command DiffOrig vert new | set bt=nofile | r ++edit # | 0d_ | diffthis
		  \ | wincmd p | diffthis
endif



set sessionoptions=blank,buffers,folds,help,tabpages,winsize " prevent sessions from saving options and working directory

" autoload the session when running vim
au VimEnter * nested :call sessionpersist#LoadSession()

" autosave the session on exiting vim
au VimLeave * nested :call sessionpersist#UpdateSession()
